#!/bin/csh

set wrapfile="vasp.wrap"

while ( $#argv != 0 )
  switch ("$1")
    case "-h":
      cat - <<EOF
runstruct_vasp [-w file] [-p] [-lu] [-ng] [-nr] [-ex] cmdprefix
  where file is an optional alternate wrap file (default: vasp.wrap)
    If the wrap file is not found in the current directory,
    it searches in the parent directories .. and ../..
    For a description of the syntax of the vasp.wrap file, type
     ezvasp -h
    and look for options under the '[INCAR]' section.

  -p means preserve vasp output file
  -lu means look up one directory for WAVECAR to use as starting point.
  -ng means do not generate a vasp.in file, use the existing one.
  -nr means do not run vasp, just generate input files
  -ex means do not generate vasp.in, do not run vasp, but extract info from vasp output file
  cmdprefix is the prefix needed for vasp to run on a remote machine,
            such as "node -s node2" 
EOF
      exit 1
      breaksw
    case "-w":
      set wrapfile="$2"
      shift
    breaksw
    case "-p":
      set preservefiles
    breaksw
    case "-lu":
      set lookup
    breaksw
    case "-ng":
      set notgenvaspin
    breaksw;
    case "-nr":
      set notrunvasp
    breaksw;
    case "-ex":
      set extractonly
    breaksw;
    default:
      break;
  endsw
  shift
end

if ( ! $?extractonly ) then 

  if ( ! -e ~/.ezvasp.rc ) then
    ezvasp
    exit
  else
    source ~/.ezvasp.rc
  endif

  if (! $?notgenvaspin ) then
    str2ezvasp $wrapfile
  endif

  if ( $?lookup ) then
    if ( -e ../WAVECAR ) then
      cp ../WAVECAR .
    endif
  endif

  set notrunvaspswitch=""

  if ( $?notrunvasp ) then
    set notrunvaspswitch="-n"
  endif

  if ( $#argv == 0 ) then
    #single machine mode if no argument
    ezvasp $notrunvaspswitch vasp.in
  else
    #multiple machine mode if argument is remote command
    ezvasp $notrunvaspswitch -p "$* $VASPCMD" vasp.in
  endif

endif

if ( ! $?notrunvasp ) then
  extract_vasp
  if ( $?lookup ) then
    cp WAVECAR ../
  endif
  if ( ! $?extractonly ) then
    if ( ( ! $?preservefiles ) && -e OUTCAR.static ) then
      cleanvasp -v
    endif
    gzip -f OUTCAR.* >& /dev/null

    checkerr_vasp -e
  endif
endif
